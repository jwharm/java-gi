import org.apache.tools.ant.taskdefs.condition.Os
import io.github.jwharm.javagi.GirParserService
import io.github.jwharm.javagi.GenerateSources

/*
 * Common build settings for Java-GI modules:
 * 
 * - Load plugins
 * - Set maven repositories
 * - Load common dependencies
 * - Set group and Java-GI version number
 * - Set JDK version
 * - Configure 'generateSources' action
 * - Set OS-specific library paths and parameters for unit tests
 * - Set common POM metadata and enable signing
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.annotations
    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
}

group = 'io.github.jwharm.javagi'
version = '0.10.2'

java {
    if (! System.getenv('CI')) {
        withJavadocJar()
    }
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

// Register a build service that will parse and cache GIR files
gradle.sharedServices.registerIfAbsent("gir", GirParserService.class) {
    it.parameters.inputDirectory = new File(rootDir, girFilesLocation)
}

// Register the task that will generate Java sources from GIR files
def generateSources = tasks.register('generateSources', GenerateSources)
generateSources.configure {
    outputDirectory = layout.buildDirectory.dir('generated/sources/java-gi')
}

// Add the generated sources to the main sourceSet
sourceSets.main.java.srcDir(generateSources)

tasks.named('compileJava', JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.named('javadoc', Javadoc) {
    if (System.getenv('CI')) {
        enabled = false
    }
    options.addStringOption('Xdoclint:none', '-quiet')
    options.encoding = 'UTF-8'
}

tasks.named('compileTestJava', JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.named('test', Test) {
    // Don't run tests in Github action. The native libraries aren't installed.
    if (System.getenv('CI')) {
        enabled = false
    }

    // Log standard output and error streams when running tests
    testLogging.showStandardStreams = true

    // Configure library path for MacOS (Homebrew) and set MacOS-specific JVM parameter
    if (Os.isFamily(Os.FAMILY_MAC)) {
        jvmArgs += '-Djava.library.path=/opt/homebrew/lib'
        jvmArgs += '-XstartOnFirstThread'
    }

    // Configure library path for Arch, Fedora and Debian/Ubuntu
    else if (Os.isFamily(Os.FAMILY_UNIX)) {
        jvmArgs += '-Djava.library.path=/usr/lib64:/lib64:/lib:/usr/lib:/lib/x86_64-linux-gnu'
    }

    // Configure library path for Windows (MSYS2)
    else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        jvmArgs += '-Djava.library.path=C:/msys64/mingw64/bin'
    }

    useJUnitPlatform()
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "${groupId}:${artifactId}"
                groupId = 'io.github.jwharm.javagi'
                description = "Java language bindings for ${artifactId.capitalize()}, generated with Java-GI"
                url = 'https://jwharm.github.io/java-gi/'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License, version 2.1'
                        url = 'https://www.gnu.org/licenses/lgpl-2.1.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jwharm'
                        name = 'Jan-Willem Harmannij'
                        email = 'jwharmannij@gmail.com'
                        url = 'https://github.com/jwharm'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/jwharm/java-gi.git'
                    developerConnection = 'scm:git:ssh://github.com:jwharm/java-gi.git'
                    url = 'http://github.com/jwharm/java-gi/tree/master'
                }
            }
        }
    }

    if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
        repositories {
            maven {
                name = 'OSSRH'
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
        signing {
            sign publishing.publications.mavenJava
        }
    }
}
